Parser:
  Title: M365 Defender Network Sessions
  Version: '0.1'
  LastUpdated: Aug 4, 2021
Product:
  Name: M365 Defender
Normalization:
  Schema: NetworkSession
  Version: '0.2.0'
References:
- Title: ASIM Network Session Schema
  Link: https://aka.ms/ASimNetworkSessionDoc
- Title: ASIM
  Link: https:/aka.ms/MsASIM
Description: |
  This Query Parser maps M365 Defender network events to the Advanced SIEM Information Model Network Session schema.
ParserName: vimNetworkSessionMicrosoft365Defender
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: dstipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: dstportnumber
    Type: int
    Default: int(null)
  - Name: url_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: httpuseragent_has_any
    Type: dynamic 
    Default: dynamic([])
  - Name: hostname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: dvcaction
    Type: dynamic
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: "'*'"
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let M365Defender=
    (starttime:datetime=datetime(null)
    , endtime:datetime=datetime(null)
    , srcipaddr_has_any_prefix:dynamic=dynamic([])
    , dstipaddr_has_any_prefix:dynamic=dynamic([])
    , dstportnumber:int=int(null)
    , url_has_any:dynamic=dynamic([])
    , httpuseragent_has_any:dynamic=dynamic([])
    , hostname_has_any:dynamic=dynamic([])
    , dvcaction:dynamic=dynamic([])
    , eventresult:string='*'
    , disabled:bool=false
   ){
  let DirectionLookup=datatable(ActionType:string,NetworkDirection:string,Outbound:boolean)[
    'ConnectionSuccess','Outbound', true
    ,'ConnectionFailed', 'Outbound', true
    ,'ConnectionRequest','Outbound', true
    ,'InboundConnectionAccepted', 'Inbound', false
    ,'ConnectionFound', 'Unknown', false
    ,'ListeningConnectionCreated', 'Listening', false 
  ];
  // -- Common preprocessing to both input and outbound events
  let RawNetworkEvents =
    DeviceNetworkEvents | where not(disabled)
    // ***************  Prefilterring *****************************************************************
    |where (isnull(starttime) or TimeGenerated>=starttime) 
           and (array_length(httpuseragent_has_any) ==0) // if filtering by ua - return nothing
           and (array_length(dvcaction)==0 )     /// if filtered by action return nothing
           and (isnull(endtime) or TimeGenerated<=endtime) 
           and (isnull(dstportnumber) or dstportnumber == LocalPort or dstportnumber == RemotePort) 
           and (array_length(url_has_any)==0  or RemoteUrl has_any (url_has_any)) 
           and (array_length(srcipaddr_has_any_prefix)==0 
                or has_any_ipv4_prefix(RemoteIP,srcipaddr_has_any_prefix)
                or has_any_ipv4_prefix(LocalIP ,srcipaddr_has_any_prefix)
                )
           and (array_length(dstipaddr_has_any_prefix)==0 
                or has_any_ipv4_prefix(RemoteIP,dstipaddr_has_any_prefix)
                or has_any_ipv4_prefix(LocalIP ,dstipaddr_has_any_prefix)
                )
           and (array_length(hostname_has_any)==0 
              or RemoteUrl has_any(hostname_has_any) or DeviceName has_any(hostname_has_any)
              ) 
    | extend EventResult = iff(ActionType=='ConnectionFailed','Failure','Success')
    | where (eventresult=='*' or EventResult==eventresult)
    // *************** / Prefilterring *****************************************************************
    | extend
      // Event
      EventOriginalUid = tostring(ReportId),
      EventCount = int(1),
      EventProduct = 'M365 Defender for Endpoint',
      EventVendor = 'Microsoft',
      EventSchema = 'NetworkSession',
      EventSchemaVersion = '0.1.0',
      EventStartTime = TimeGenerated,
      EventEndTime = TimeGenerated,
      EventType = 'NetworkSession',
      EventSeverity = "Informational",
      DvcIdType = 'MDEid'
    | project-rename
      NetworkProtocol = Protocol,
      DvcFQDN = DeviceName
    | extend
      User = iff (InitiatingProcessAccountDomain == '', InitiatingProcessAccountName, strcat(InitiatingProcessAccountDomain, '\\', InitiatingProcessAccountName)),
      UsernameType = iff(InitiatingProcessAccountDomain == '','Simple', 'Windows'),
      SplitHostname = split(DvcFQDN,"."),
      SplitUrl = split(RemoteUrl,".")
    | extend  
      DvcHostname = tostring(SplitHostname[0]),
      DvcDomain = strcat_array(array_slice(SplitHostname, 1, -1), '.'),
      UrlHostname = tostring(SplitUrl[0]),
      UrlDomain = strcat_array(array_slice(SplitUrl, 1, -1), '.'),
      SrcDomainType = "FQDN",
      DvcDomainType = "FQDN",
      DstDomainType = "FQDN",
      DvcIpAddr = LocalIP
    | extend
      Dvc = DvcHostname  
    | project-rename
      DvcId = DeviceId
    | project-away SplitUrl, SplitHostname
  ;
  let OutboundNetworkEvents = 
    RawNetworkEvents
    // *************** Postfilterring *****************************************************************
    | where (isnull(dstportnumber) or dstportnumber==RemotePort)
        and (array_length(hostname_has_any)==0 or UrlHostname has_any (hostname_has_any))
    // *************** /Postfilterring *****************************************************************
    | lookup DirectionLookup on ActionType
    | where Outbound
    | project-rename
       DstIpAddr = RemoteIP,
       SrcIpAddr = LocalIP,
       DstPortNumber = RemotePort,
       SrcPortNumber = LocalPort,
       SrcUsernameType = UsernameType,
       SrcUserAadId = InitiatingProcessAccountObjectId,
       SrcUserId = InitiatingProcessAccountSid,
       SrcUserUpn = InitiatingProcessAccountUpn,
       SrcUserDomain = InitiatingProcessAccountDomain
    | extend
       SrcUsername = User,
       SrcDvcId = DvcId,
       SrcDvcIdType = 'MDEid',
       SrcUserIdType = "SID",
      DstHostname = UrlHostname
    | project-rename
       DstDomain = UrlDomain,
       DstFQDN = RemoteUrl
    | extend  
       SrcHostname = DvcHostname,
       SrcDomain = DvcDomain,
       SrcFQDN = DvcDomain
    // Processes
    | extend
       SrcProcessId = tostring(InitiatingProcessId),
       ParentProcessId = tostring(InitiatingProcessParentId)
    | project-rename
       SrcProcessName = InitiatingProcessFileName,
       SrcProcessCommandLine = InitiatingProcessCommandLine,
       SrcProcessCreationTime = InitiatingProcessCreationTime,
       SrcProcessIntegrityLevel = InitiatingProcessIntegrityLevel,
       SrcProcessTokenElevation = InitiatingProcessTokenElevation,
       ParentProcessName = InitiatingProcessParentFileName,
       ParentProcessCreationTime = InitiatingProcessParentCreationTime
       // SrcProcessFileSize = InitiatingProcessFileSize,
       // SrcProcessCompany = InitiatingProcessVersionInfoCompanyName,
       // SrcProcessFileProduct = InitiatingProcessVersionInfoProductName,
       // SrcProcessFileVersion = InitiatingProcessVersionInfoProductVersion,
       // SrcProcessFileInternalName = InitiatingProcessVersionInfoInternalFileName,
       // SrcProcessFileOriginalName = InitiatingProcessVersionInfoOriginalFileName,
       // SrcProcessFileDescription = InitiatingProcessVersionInfoFileDescription
    | extend
       Process = SrcProcessName,
       ProcessId = SrcProcessId,
       SrcAppName = SrcProcessName,
       SrcAppType = "Process"
  ;
  let InboundNetworkEvents = 
    RawNetworkEvents
    // *************** Postfilterring *****************************************************************
    | where (isnull(dstportnumber) or dstportnumber==LocalPort)
        and (array_length(hostname_has_any)==0 or DvcHostname has_any (hostname_has_any))
    // *************** /Postfilterring *****************************************************************
    | lookup DirectionLookup on ActionType
    | where not(Outbound)
    | project-rename
       SrcIpAddr = RemoteIP,
       DstIpAddr = LocalIP,
       SrcPortNumber = RemotePort,
       DstPortNumber = LocalPort,
       DstUsernameType = UsernameType,
       DstUserAadId = InitiatingProcessAccountObjectId,
       DstUserId = InitiatingProcessAccountSid,
       DstUserUpn = InitiatingProcessAccountUpn,
       DstUserDomain = InitiatingProcessAccountDomain
    | extend
       DstUsername = User,
       DstDvcId = DvcId,
       DstDvcIdType = 'MDEid',
       DstUserIdType = 'SID'
   | project-rename
       SrcHostname = UrlHostname,
       SrcDomain = UrlDomain,
       SrcFQDN = RemoteUrl
    | extend  
       DstHostname = DvcHostname,
       DstDomain = DvcDomain,
       DstFQDN = DvcFQDN
    // Processes
    | extend
       DstProcessId = tostring(InitiatingProcessId),
       ParentProcessId = tostring(InitiatingProcessParentId)
    | project-rename
       DstProcessName = InitiatingProcessFileName,
       DstProcessCommandLine = InitiatingProcessCommandLine,
       DstProcessCreationTime = InitiatingProcessCreationTime,
       DstProcessIntegrityLevel = InitiatingProcessIntegrityLevel,
       DstProcessTokenElevation = InitiatingProcessTokenElevation,
       ParentProcessName = InitiatingProcessParentFileName,
       ParentProcessCreationTime = InitiatingProcessParentCreationTime
       // SrcProcessFileSize = InitiatingProcessFileSize,
       // SrcProcessCompany = InitiatingProcessVersionInfoCompanyName,
       // SrcProcessFileProduct = InitiatingProcessVersionInfoProductName,
       // SrcProcessFileVersion = InitiatingProcessVersionInfoProductVersion,
       // SrcProcessFileInternalName = InitiatingProcessVersionInfoInternalFileName,
       // SrcProcessFileOriginalName = InitiatingProcessVersionInfoOriginalFileName,
       // SrcProcessFileDescription = InitiatingProcessVersionInfoFileDescription
    | extend
       Process = DstProcessName,
       ProcessId = DstProcessId,
       DstAppName = DstProcessName,
       DstAppType = "Process"
  ;
  union InboundNetworkEvents, OutboundNetworkEvents
  | extend // aliases
     Hostname = DstHostname,
     IpAddr = SrcIpAddr,
     Src = SrcIpAddr,
     Dst = DstIpAddr 
  };
  M365Defender(starttime, endtime, srcipaddr_has_any_prefix, dstipaddr_has_any_prefix, dstportnumber, url_has_any, httpuseragent_has_any, hostname_has_any, dvcaction, disabled)