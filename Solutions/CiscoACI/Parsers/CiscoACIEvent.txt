// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as CiscoACIEvent and specifying Legacy Category.
// This function maps Cisco SEG Consolidated Event Logs to normalization schema (https://docs.microsoft.com/azure/sentinel/normalization-schema).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. CiscoACIEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
Syslog
| where SyslogMessage contains_cs ('%LOG_LOCAL') and SyslogMessage contains ('SYSTEM_MSG')
| extend EventVendor = 'Cisco'
| extend EventProduct = 'ACI'
| extend EventData = split(SyslogMessage, ' ')
| extend DvcHostname = tostring(EventData[1])
| extend EventProp = split(replace(@'\]$', '', replace(@'\A\[', '', tostring(EventData[3]))), '][')
| extend EventId = case(array_length(EventProp) != 3, tostring(EventProp[0]),
                        "")
| extend DstResourceId = tostring(EventProp[-1])
| extend EventSeverity = tostring(EventProp[-2])
| extend EventResultDetails = case(array_length(EventProp) == 5, tostring(EventProp[1]),
                                    "")
| extend EventResult = case(array_length(EventProp) == 5, tostring(EventProp[2]),
                            array_length(EventProp) == 4, tostring(EventProp[1]),
                            "")
| extend EventMessage = extract(@'.*\]\s(.*)', 1, SyslogMessage)
| project-away SyslogMessage, EventData, EventProp