// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as JBossEvent and specifying Legacy Category.
// This function maps JBoss Enterprise Application Platform events to Azure Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. JBossEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
JBossLogs_CL
| extend EventVendor = 'Red Hat'
| extend EventProduct = 'JBoss Enterprise Application Platform'
| extend EventCount = 1
| extend EventSchemaVersion = 0.2
| extend EventSeverity = extract(@'\d{2},\d{3}\s(\w+)\s+\[', 1, RawData)
| extend ActingProcessName = extract(@'\[([A-Za-z0-9.-]+)\]', 1, RawData)
| extend ActingProcessThread = extract(@'\]\s+\((.*?)\)', 1, RawData)
| extend EventOriginalType = extract(@"([A-Z]+[0-9]+)(:)",1, RawData)
| extend EventMessage = extract(@'\)\s([A-Z0-9]+:\s)?(.*)', 2, RawData)
| project-away RawData
