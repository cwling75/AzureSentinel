// Title:           Semperis Directory Services Protector (DSP) Data Parser
// Author:          Semperis
// Version:         1.0
// Last Updated:    04/14/2021
// Comment:         version 1.0
//
// DESCRIPTION:
// This parser takes Semperis DSP Windows event logs from the relevant connector's data stream and parses the data into a normalized schema
//
// USAGE:
// 1. Open Log Analytics/Azure Sentinel Logs blade. Copy the query below and paste into the Logs query window.
// 2. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter a Function Name.
//    In order for the Semperis DSP Windows Event logs to work with pre-built queries and workbooks the Function Alias must be set to - dsp_parser
// 3. Function App usually take 5-10 minutes to activate. You can then use Function Alias for other queries
//
//
// REFERENCE:
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
//
//
Event
| where Source == "Semperis-DSP-Security"
| where EventID in ("9211","9212","9208")
| parse EventData with 
    '<DataItem type="' EventDataType 
    '" time="' EventDataTime 
    '" sourceHealthServiceId="' EventDataSourceHealthServiceId 
    '"><EventData ' * '>' DSPData '</EventData></DataItem>'
| parse DSPData with 
    *
    '<Data Name="firstFound">' FirstFound '</Data>'
    * 
| parse DSPData with 
    '<Data Name="generationTime">' GenerationTime '</Data>'
    '<Data Name="securityIndicatorName">' SecurityIndicatorName '</Data>'
	'<Data Name="result">' Result '</Data>' 
    *
	'<Data Name="score">' Score '</Data>'
	'<Data Name="forestName">' ForestName '</Data>'
	'<Data Name="domains">' Domains '</Data>'
	'<Data Name="severity">' Severity '</Data>'
	'<Data Name="weight">' Weight '</Data>'
	'<Data Name="securityFrameworkTags">' SecurityFrameworkTags '</Data>'
	'<Data Name="securityIndicatorDescription">' SecurityIndicatorDescription '</Data>'
	'<Data Name="likelihoodOfCompromise">' LikelihoodOfCompromise '</Data>'
	'<Data Name="resultMessage">' ResultMessage '</Data>'
	'<Data Name="numberOfResults">' NumberOfResults '</Data>'
	'<Data Name="remediation">' Remediation '</Data>'
	'<Data Name="schedule">' Schedule '</Data>'
    * 
| extend SecurityFrameworkTagsCsv = replace(@' Mitre:', @'', tostring(SecurityFrameworkTags))
| extend SecurityFrameworkTagsCsv = replace(@'Mitre:', @'', tostring(SecurityFrameworkTagsCsv))
| extend SecurityFrameworkTags = replace(@'Mitre:', @'', tostring(SecurityFrameworkTags))
